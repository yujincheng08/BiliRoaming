apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "me.iacn.biliroaming"
        minSdkVersion 21
        targetSdkVersion 30  // Target Android R
        versionCode appVerCode.toInteger()
        versionName appVerName
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == 'release') {
                outputFileName = "BiliRoaming_${defaultConfig.versionName}.apk"
            }
        }
    }

    sourceSets {
        main {
            proto {
                srcDir 'src/main/proto'
                include '**/*.proto'
            }
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.11.4'
    }

    generatedFilesBaseDir = "$projectDir/src/generated"

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
    compileOnly 'com.android.support:support-annotations:28.0.0'
    implementation 'com.google.protobuf:protobuf-javalite:3.13.0'
    compileOnly 'com.google.protobuf:protoc:3.13.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'
}

afterEvaluate {
    installDebug.doLast {
        restartBiliBili()
    }
}
repositories {
    mavenCentral()
}

def restartBiliBili() {
    exec {
        commandLine android.adbExecutable, 'shell', 'am', 'force-stop', 'tv.danmaku.bili'
    }
    exec {
        commandLine android.adbExecutable, 'shell', 'monkey', '-p', 'tv.danmaku.bili', '1'
    }
}
